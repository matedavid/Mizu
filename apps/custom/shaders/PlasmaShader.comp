#version 450

layout (push_constant) uniform PlasmaInfo {
	uint width;
	uint height;
	float time;
} uPlasmaInfo;

layout (set = 0, binding = 0, rgba8) uniform writeonly image2D uOutput;

const float PI = 3.14159265;

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
void main() {
    ivec2 gid = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv = vec2(gid) / vec2(uPlasmaInfo.width, uPlasmaInfo.height);

    float t = uPlasmaInfo.time * 10.0;
    const vec2 vp = vec2(512.0, 512.0);

    vec2 p0 = (uv - 0.5) * vp;
    vec2 hvp = vp * 0.5;
    vec2 p1d = vec2(cos( t / 98.0),  sin( t / 178.0)) * hvp - p0;
    vec2 p2d = vec2(sin(-t / 124.0), cos(-t / 104.0)) * hvp - p0;
    vec2 p3d = vec2(cos(-t / 165.0), cos( t / 45.0))  * hvp - p0;
    float sum = 0.5 + 0.5 * (
        cos(length(p1d) / 30.0) +
        cos(length(p2d) / 20.0) +
        sin(length(p3d) / 25.0) * sin(p3d.x / 20.0) * sin(p3d.y / 15.0));
        
    float i = fract(sum);
    vec3 t2 = (64.0 * uPlasmaInfo.time) / vec3(63.0, 78.0, 45.0);
    vec3 cs = cos(i * PI * 2.0 + vec3(0.0, 1.0, -0.5) * PI + t2);
    cs = 0.5 * cs + 0.5;

    imageStore(uOutput, gid, vec4(cs, cs.x));
}